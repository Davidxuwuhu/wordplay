===Maze\en
`
A maze is a 2D grid of emoji, with some functions for checking the state at particular grid postions.
Mazes are generated randomly.
`/en
•Maze() (
	grid: 
		[
			[ '🎬' '❤️' '❌'   ø    ø]
			[ '❤️'   ø    ø    ø    ø]
			[ '🐈' '😀' '💬' '🫥' '😞']
			[   ø  '👍'  ø    ø   '😕']
			[ '🤩' '🥹'  ø   '😫' '😡']
    	]
 
	ƒ cell(row•# column •#) (
		cells: grid[row]
		cells•ø ? ø cells[column] 
	)

	ƒ allowed(row•# column•#) cell(row column)•''
)

maze: Maze()

•State(row•# column•# steps•#) 
(
	ƒ down() State(row + 1 column steps + 1)
	ƒ up() State(row - 1 column steps + 1)
	ƒ left() State(row column - 1 steps + 1)
	ƒ right() State(row column + 1 steps + 1)
)

ƒ  allowed(state•State row•# col•#) maze.allowed(state.row + row state.column + col)

direction: choice()
state•State:  ∆ direction ? State(1 1 0) …
		((direction = "left") & allowed(state 0 -1)) ? state.left()
			((direction = "right") & allowed(state 0 1)) ? state.right()
				((direction = "up") & allowed(state -1 0)) ? state.up()
					((direction = "down") & allowed(state 1 0)) ? state.down()
						state

current: ( cell: maze.cell(state.row state.column) cell•ø ? ' ' cell)
up: ( cell: maze.cell(state.row - 1 state.column) cell•ø ? ' ' '↑')
down: ( cell: maze.cell(state.row + 1 state.column) cell•ø ? ' ' '↓')
left: ( cell: maze.cell(state.row state.column - 1) cell•ø ? ' ' '←')
right: ( cell: maze.cell(state.row state.column + 1) cell•ø ? ' ' '→')
won: current = '🤩'

Verse(
	[
		Group(
			Row(0m) 
			[
				Phrase(left name: 'left' selectable: left.length() > 0 place: Place(-1m 1m))				 
				Group(
					Stack(0m) 
					[
						Phrase(up name: 'up' selectable: up.length() > 0)
						Phrase(current rest: won ? 
Sequence({ 
0%: Pose(tilt: 359°) 
100%:🤪(tilt: 0°)
} ⏳: 1s style: 'straight') 
Pose())
						Phrase(down name: 'down' selectable: down.length() > 0)
					]
				)
				Phrase(right name: 'right' selectable: right.length() > 0 place: Place(1m 1m))
			]
	  )
	]
  place: Place(0m 0m -3m)
	font: 'Noto Emoji'
  background: Color(0 0 0°)
	rest: Pose(Color(100 100 0°))
)