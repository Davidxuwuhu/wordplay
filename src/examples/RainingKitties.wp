=== Raining Kitties
count: 100
size: 30

•Kitty(x•# y•# z•# angle•# speed•#) ()

`Initially, make a list of 20 kitties, each placed somewhere random.
When the the clock ticks, move them all down a bit.`
kitties•[Kitty]:  ∆ time() ? 
    count → [].translate(
        ƒ(_) Kitty(
            random(-size size)
            random(-size size)
            random(0 size)
            random(360)
            random(-30 30)
        )
    )
    …
    kitties.translate(
        ƒ(kitty•Kitty)
            (kitty.y: kitty.y < -size ? size kitty.y - 2)
                .angle: kitty.angle + kitty.speed
    )

`Convert the kitties into phrases`
Verse(
    kitties.translate(
        ƒ(kitty•Kitty) 
            Phrase(
                "🐈" 
                size: 5m 
                place: Place(
                    kitty.x · 1m
                    kitty.y · 1m
                    kitty.z · 1m
                    kitty.angle ·1°
                )
            )
    )
)