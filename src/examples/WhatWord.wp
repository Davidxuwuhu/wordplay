=== WhatWord
↓ ⌨️
↓ words
`a list of guesses and a secret word`/en
`El estado del juego es una lista de conjeturas y una palabra secreta.`/es
`🧩 = 🤔 + 🤫`/😀
•Game/en, 🧩/😀(guesses/en,🤔/😀•[""] secret/en, 🤫/😀•"") (
  guessesRemaining/en,🔢/😀: (secret.📏() · 2) - guesses.📏()
  status/en,🚨/😀: 
    secret = "" ? "start"
    secret→[""].all(ƒ(letter•"") guesses.has(letter)) ? "won"
    guessesRemaining ≤ 0 ? "lost"
    "playing"
)

start/en,🟢/😀: Game([] "")

key: ⌨️()

`When the keyboard state changes, the game state changes.`/en
`Cuando cambia el estado del teclado, cambia el estado del juego.`/es
`⌨️ + 🤔 + 🤫 → ⚽️`/😀
state/en,🧠/😀•Game: ∆ key ? start … 
    key = "Escape" ? start
    (state.status ≠ "playing") & (key = " ") ? Game([] words.random())
    (state.status = "playing") & (key.length() = 1) & ~state.guesses.has(key) ? 
      Game(state.guesses.add(key) state.secret)
      state

`The board is a translation of the secret, accounting for guesses`/en
`El tablero es una traducción del secreto, teniendo en cuenta las conjeturas.`/es
`🤔 + 🤫 → 🔤`/😀
board/en,🔤/😀: 💬(
  state.secret→[""].translate(ƒ(letter•"") ((state.status = "lost") | state.guesses.has(letter)) ? letter ".").join(' ')
  2m
)

sway: { 
  0%: Pose(offset: Place(rotation: -2°))
  50%: Pose(offset: Place(rotation: 2°)) 
  100%: Pose(offset: Place(rotation: -2°))
} 

bounce: {
  0%: Pose(scale: 1 offset: Place(y: 0m))
  10%: Pose(scale: 1.1 offset: Place(y: 2m))
  30%: Pose(scale: .9 offset: Place(y: 2m))
  50%: Pose(scale: 1 offset: Place(y: 0m))
  57%: Pose(scale: 1 offset: Place(y: .1m))
  64%: Pose(scale: 1 offset: Place(y: 0m))
  100%: Pose(scale: 1 offset: Place(y: 0m))
}

fade: Pose(opacity: 0)

screen: 
  state.status = "start" ? 
    Group(⬇() [
      💬(["What***Word***"/en "Que***Palabra***"/es "?_"/😀] 
        3m 
        place:📍(z: -1m) 
        enter: Pose(opacity: 0%)
        rest: Sequence(sway 1s)
      )
      💬("Press ***space*** to begin")
    ] 
    exit: fade
  )
  state.status = "lost" ? 
    Group(⬇() [
      board 
      💬("You lost. Press **space** to play again." enter: Pose(offset: Place(rotation: 0°)) rest: Pose(offset: Place(rotation: -5°)))
    ] exit: fade)
  state.status = "won" ?
    Group(⬇() [
        board.rest:Sequence(bounce 1s) 
        💬("You won, nice job! Press space to play again.")
    ] exit: fade)
    Group(⬇() [
      board 
      💬(state.guesses.length() = 0 ? "Type a letter to guess" "Guesses: \state.guesses.join(' ')\")
      💬(
        "\state.guessesRemaining→''\ remaining"
        rest: Sequence(sway (state.guessesRemaining ÷ 5) ·1s)
      )
    ])

`Create a world with the screen in it`/en
Verse(screen font: "Poor Story" background: 🌈(0% 0 0°) rest: Pose(color: 🌈(100% 0 0°)))

=== words
`These are the secret words. Don't tell anyone!`/en
`Estas son las palabras secretas. ¡No se lo digas a nadie!`/es
`🤫`/😀
[
  'kitty'
  'house'
  'heat'
  'farm'
  'townhouse'
  'heatwave'
]