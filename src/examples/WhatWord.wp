=== WhatWord
↓ keyboard
↓ words
`The game state is a list of guesses and a secret word.`/eng
`El estado del juego es una lista de conjeturas y una palabra secreta.`/spa
`🧩 = 🤔 + 🤫`/😀
•Game/eng, 🧩/😀(guesses/eng,🤔/😀•[""] secret/eng, 🤫/😀•"") (
  guessesRemaining/eng,🔢/😀: (secret.length() · 2) - guesses.length()
  status/eng,🚨/😀: 
    secret = "" ? "start"
    secret→[""].all(ƒ(letter•"") guesses.has(letter)) ? "won"
    guessesRemaining ≤ 0 ? "lost"
    "playing"
)

start/eng,🟢/😀: Game([] "")

`When the keyboard state changes, the game state changes.`/eng
`Cuando cambia el estado del teclado, cambia el estado del juego.`/spa
`⌨️ + 🤔 + 🤫 → ⚽️`/😀
state/eng,⚽️/😀•Game: start …
    keyboard.key = "Escape" ? start
    (state.status ≠ "playing") & (keyboard.key = " ") & keyboard.down ? Game([] words.random())
    (state.status = "playing") & keyboard.down & (keyboard.key.length() = 1) & ~state.guesses.has(keyboard.key) ? 
      Game(state.guesses.add(keyboard.key) state.secret)
      state

`The board is a translation of the secret, accounting for guesses`/eng
`El tablero es una traducción del secreto, teniendo en cuenta las conjeturas.`/spa
`🤔 + 🤫 → 🔤`/😀
board/eng,🔤/😀: Phrase(
  state.secret→[""].translate(ƒ(letter•"") ((state.status = "lost") | state.guesses.has(letter)) ? letter ".").join(' ') 
  2m
)

screen: state.status = "start" ? Stack(Phrase(["What***Word***"/eng "???"/😀] 3m) Phrase("Press ***space*** to begin"))
state.status = "lost" ?  Stack(board Phrase("You lost. Press **space** to play again."))
state.status = "won" ?   Stack(board Phrase("You won, nice job! Press space to play again."))
                         Stack(
                            board 
                            Phrase(state.guesses.length() = 0 ? "Type a letter to guess" "Guesses: \state.guesses.join(' ')\")
                            Phrase("\state.guessesRemaining→''\ remaining")
                          )

`Create a world with the screen in it`/eng
Verse(screen "Poor Story" Color(100% 0 0°) Color(0% 0 0°))

=== words
`These are the secret words. Don't tell anyone!`/eng
`Estas son las palabras secretas. ¡No se lo digas a nadie!`/spa
`🤫`/😀
[
  'kitty'
  'house'
  'heat'
  'farm'
  'townhouse'
  'heatwave'
]