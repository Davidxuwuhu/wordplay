Move
=== start
•Cat(position•#m direction•#)

key: Key()
speed: 1m

cat•Cat:  Cat(5m 1) … ∆ key …
    key = "ArrowLeft" ? Cat(cat.position - speed 1) 
    	key = "ArrowRight" ? Cat(cat.position + speed -1) 
    		cat

ƒ close(ball•Place|ø x•#m) 
    ball•ø ? ⊥ 
    (ball.y < 1m) &
    (
        ((ball.x - x).positive() < 1m) | 
        ((ball.x - (x + 2m)).positive() < 1m)
    )

ball: Motion(
    Phrase("o") 
     startplace: Place(0m 0m)
     vx: ◆ ? 0m/s
         close(ball.place cat.position) ? (Random(1 5) · -1m/s · cat.direction) 
          ø
     vy: ◆ ? 0m/s 
         close(ball.place cat.position) ? (Random(2 10) · 1m/s) 
          ø
     vangle: ◆ ? 0°/s 
         close(ball.place cat.position) ? (cat.direction · 120°/s) 
          ø
)

Stage([
		Phrase(
    	"🐈"
        face: "Noto Color Emoji"
    	name: "kitty"
    	size: 2m
    	place: Place(cat.position 0m)
    	resting: Pose(flipx: cat.direction < 0)
    	moving: Sequence(sway(5°) 0.25s "zippy")
) ball] place: Place(0m 0m -5m))